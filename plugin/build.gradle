plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

repositories {
    maven { url 'https://jitpack.io' }
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
}

dependencies {
    implementation project(':worldeditselectionvisualizer-compatibility-commons')
    implementation project(':worldeditselectionvisualizer-compatibility-v6')
    implementation project(':worldeditselectionvisualizer-compatibility-v7')
    implementation 'fr.mrmicky:fastparticles:2.0.2'
    compileOnly 'com.sk89q.worldedit:worldedit-bukkit:7.2.0'
    compileOnly 'me.clip:placeholderapi:2.11.6'

    testImplementation 'net.md-5:bungeecord-chat:1.20-R0.2'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.13.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

shadowJar {
    archiveFileName = "WorldEditSelectionVisualizer-${project.version}.jar"

    relocate 'fr.mrmicky.fastparticles', 'fr.mrmicky.worldeditselectionvisualizer.particles'

    manifest {
        attributes 'Paperweight-Mappings-Namespace': 'mojang'
    }
}

artifacts {
    archives shadowJar
}

processResources {
    filesMatching('plugin.yml') {
        expand 'pluginVersion': project.version
    }
}

test {
    useJUnitPlatform()
}
